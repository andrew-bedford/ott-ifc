metavar variable, x ::= {{ coq nat }} {{ coq-equality }} {{ lex alphanum }} 
metavar integer, n ::= {{ coq nat }} {{ lex  numeral }}
indexvar index, i, j ::= {{ coq nat }}

grammar
arith_expr, a :: ae_ ::=
    | x                                 ::  :: variable
    | n                                 ::  :: int
    | a1 + a2                           ::  :: addition
    | a1 * a2                           ::  :: multiplication

bool_expr, b :: be_ ::=
    | true                              ::  :: true
    | false                             ::  :: false
    | a1 < a2                           ::  :: less_than

commands, c :: cmd_ ::=
    | stop                              ::  :: stop
    | skip                              ::  :: skip
    | x := a                            ::  :: assignment
    | c1 ; c2                           ::  :: sequence
    | if b then c1 else c2 end          ::  :: if
    | while b do c end                  ::  :: while 

memory, m :: memory_ ::=
   | empty                              ::  :: empty
   | m [ x |-> n ]                      ::  :: cell

formula :: formula_ ::=
   | judgement                          ::  :: judgement
   | m ( x ) = n                        ::  :: lookup
   | n1 + n2 = n3                       ::  :: addition
   | n1 * n2 = n3                       ::  :: multiplication
   | n1 < n2 = true                     ::  :: lt_true
   | n1 < n2 = false                    ::  :: lt_false

terminals :: terminals_ ::=
  |  ->                                ::   :: arrow      {{ tex \rightarrow }}
  | |-                                 ::   :: turnstile  {{ tex \vdash }}
  | |->                                ::   :: mapsto     {{ tex \mapsto }}
  | -->                                ::   :: red        {{ tex \longrightarrow }}
  | '{'                                ::   :: leftbrace  {{ tex \{ }}
  | '}'                                ::   :: rightbrace {{ tex \} }}
  | '<'                                ::   :: leftangle {{ tex \langle}}
  | '>'                                ::   :: rightangle {{ tex \rangle}}

%----------------------------------------------------------------%
%                    SMALL-STEP SEMANTICS                        %
%----------------------------------------------------------------%
defns
Jop :: '' ::= 

%%%%%%%%%%%%%%%%%%%%%%%%%%% Arithmetic Expressions %%%%%%%%%%%%%%%%%%%%%%%%%%%
defn 
< a , m > --> < a' , m' > :: :: arith_ops :: 'aexp_' by

----------------- :: int_constant
<n, m> --> <n, m>

m(x) = n
----------------- :: lookup
<x, m> --> <n, m>

%%% Addition %%%
<a1, m> --> <a1', m>
------------------------------ :: add_aexp_aexp
<a1 + a2, m> --> <a1' + a2, m>

<a2, m> --> <a2', m>
----------------------------- :: add_int_aexp
<n1 + a2, m> --> <n1 + n2, m>

n1 + n2 = n3
------------------------:: add_int_int
<n1 + n2, m> --> <n3, m>

%%% Multiplication %%%
<a1, m> --> <a1', m>
------------------------------ :: mult_aexp_aexp
<a1 * a2, m> --> <a1' * a2, m>

<a2, m> --> <a2', m>
----------------------------- :: mult_int_aexp
<n1 * a2, m> --> <n1 * n2, m>

n1 * n2 = n3
------------------------ :: mult_int_int
<n1 * n2, m> --> <n3, m>

%%%%%%%%%%%%%%%%%%%%%%%%%%% Boolean Expressions %%%%%%%%%%%%%%%%%%%%%%%%%%%
defn 
< b , m > --> < b' , m' > :: :: bool_ops :: 'bexp_' by

<a1, m> --> <a1', m>
------------------------------ :: lt_aexp_aexp
<a1 < a2, m> --> <a1' < a2, m>

<a2, m> --> <a2', m>
----------------------------- :: lt_int_aexp
<n1 < a2, m> --> <n1 < n2, m>

n1 < n2 = true
------------------------ :: lt_int_int
<n1 < n2, m> --> <true, m>

n1 < n2 = false
------------------------ :: lt_int_int
<n1 < n2, m> --> <false, m>

%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%% Commands %%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
defn 
< c , m > --> < c' , m' > :: :: cmd_ops :: 'cmd_' by

%%% Assignment %%%
<a, m> --> <a', m>
----------------------------- :: assign_aexp
<x := a, m> --> <x := a', m>

----------------------------- :: assign_int
<x := n, m> --> <stop, m[x |-> n]>

%%% Sequence %%%
<c1, m> --> <c1', m'>
-------------------------------- :: seq1
<c1 ; c2, m> --> <c1' ; c2, m'>

--------------------------- :: seq2
<skip ; c2, m> --> <c2, m>


%%% If %%%
<b, m> --> <b', m>
---------------------------------------------------------------- :: if_eval
<if b then c1 else c2 end, m> --> <if b' then c1 else c2 end, m>

---------------------------------------------------------------- :: if_true
<if true then c1 else c2 end, m> --> <c1, m>

---------------------------------------------------------------- :: if_false
<if false then c1 else c2 end, m> --> <c2, m>

%%% While %%%
--------------------------------------------------------------------------- :: while
<while b do c end, m> --> <if b then c ; while b do c end else skip end, m>
